plugins {
	id 'org.springframework.boot' version '2.4.4'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'java'
}

apply plugin: 'io.spring.dependency-management'

group = 'pl.allegrotech'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '15'

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.apache.httpcomponents:httpclient:4.5.13'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

configurations {
	integrationCompile.extendsFrom testImplementation
	integrationRuntimeOnly.extendsFrom testRuntimeOnly
}

tasks.withType(Test) {
	useJUnitPlatform()
}

sourceSets {
	integration {
		java {
			compileClasspath += main.output + test.output
			runtimeClasspath += main.output + test.output
			srcDir file('src/integration/java')
		}
		resources.srcDir file('src/integration/resources')
	}
}

task integration(type: Test) {
	description = 'Run the integration tests.'
	group = 'verification'
	testClassesDirs = sourceSets.integration.output.classesDirs
	classpath = sourceSets.integration.runtimeClasspath
}

check.dependsOn(integration, test)
